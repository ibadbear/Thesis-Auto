
allprojects {
    ext.artifactGroup = 'com.company.thesisauto'
    ext.artifactVersion = '0.1'
    ext.isSnapshot = true
    ext.tomcatDir = "$rootDir/deploy/tomcat"
    ext.copyright = '''Copyright (c) ${YEAR} ${PACKAGE_NAME}''' // Copyright Notice for IDEA project
    ext.classComment = '''/**
 * @author ${USER}
 */'''
    
}

buildscript {
    ext.thesisVersion = '4.4.1'
    ext.cubaVersion = '5.9.10'
    
    ext.autoConfigurePortal = true
    ext.autoConfigureMobile = true
    ext.autoConfigureMobileRest = true
    repositories {
        mavenLocal()
        maven {
            url 'http://deprecated-repo.haulmont.com:8587/nexus/content/groups/work'
            credentials {
                username(rootProject.hasProperty('repoUser') ?
                        rootProject['repoUser'] : 'external')
                password(rootProject.hasProperty('repoPass') ?
                        rootProject['repoPass'] : 'aiva33n8')
            }
        }
    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
        classpath "com.haulmont.thesis.gradle:thesis-plugin:$thesisVersion"
    }
}

def globalModule = project(':thesisauto-global')
def coreModule = project(':thesisauto-core')
def guiModule = project(':thesisauto-gui')
def webModule = project(':thesisauto-web')
def portalModule = project(':thesisauto-portal')
def mobileModule = project(':thesisauto-mobile')

def postgres = 'postgresql:postgresql:9.1-901.jdbc4'
def servletApi = 'org.apache.tomcat:servlet-api:6.0.20'




apply(plugin: 'idea')
apply(plugin: 'cuba')
apply(plugin: 'thesis')

configure([globalModule, coreModule, guiModule, webModule, portalModule, mobileModule]) {
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'idea')
    apply(plugin: 'cuba')
    apply(plugin: 'groovy')

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    sourceSets {
        main {
            groovy {
                srcDir 'src'
                exclude '**/appfolders/**/*.groovy'
                exclude '**/process/**'
                exclude '**/scripts/**'
            }
        }
        test {
            groovy { srcDir 'test' }
        }
    }

    compileGroovy {
        groovyOptions.encoding = 'UTF-8'
        options.encoding = 'UTF-8'
    }

    compileTestGroovy {
        groovyOptions.setFork(false)
        groovyOptions.encoding = 'UTF-8'
        options.encoding = 'UTF-8'
    }

    dependencies {
        testCompile('junit:junit:4.5')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure(globalModule) {
    dependencies {
        compile("com.haulmont.thesis.app:thesis-global:$thesisVersion")

    }

    task enhance(type: CubaEnhancing) {
        persistenceXml = "$globalModule.projectDir/src/persistence.xml"
    }
}

configure(coreModule) {

    configurations {
        jdbc
        dbscripts
        webcontent
    }

    dependencies {
        compile(globalModule)
        provided(servletApi)
        jdbc(postgres)
        testRuntime(postgres)
        webcontent("com.haulmont.thesis.app:thesis-core:$thesisVersion:web@zip")
        compile("com.haulmont.thesis.app:thesis-core:$thesisVersion")
        dbscripts("com.haulmont.thesis.app:thesis-core:$thesisVersion:db@zip")

    }

    task cleanConf(description: 'Cleans up conf directory') << {
        def dir = new File(tomcatDir, '/conf/app-core')
        if (dir.isDirectory()) {
            ant.delete(includeemptydirs: true) {
                fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties /process/**/* app.license')
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = 'app-core'
        appJars('thesis-core', 'thesis-global', 'reports-global', 'reports-core', 'charts-global', 'workflow-global', 'workflow-core', 'fts-core', 'fts-global', 'cuba-global', 'cuba-core', 
                'thesisauto-global', 'thesisauto-core', 'thesis-shared-workflow')
    }

    task createDb(dependsOn: assemble, description: 'Creates local database', type: CubaDbCreation) {
        dbms = 'postgres'
        host = 'localhost'
        dbName = 'thesisauto'
        dbUser = 'cuba'
        dbPassword = 'cuba'
    }

    task updateDb(dependsOn: assemble, description: 'Updates local database', type: CubaDbUpdate) {
        dbms = 'postgres'
        host = 'localhost'
        dbName = 'thesisauto'
        dbUser = 'cuba'
        dbPassword = 'cuba'
    }
}

configure(guiModule) {
    dependencies {
        compile(globalModule)
        compile("com.haulmont.thesis.app:thesis-gui:$thesisVersion")

    }

    task deployConf(type: Copy) {
        from file('src')
        include "com/company/thesisauto/**"
        into "$tomcatDir/conf/app"
    }
}

configure(webModule) {
    configurations {
        webcontent
        
        staticContent
        themes
    }

    dependencies {
        provided(servletApi)
        compile(globalModule)
        compile(guiModule)
        compile("com.haulmont.thesis.app:thesis-web:$thesisVersion")
        
        webcontent("com.haulmont.cuba:cuba-web6:$cubaVersion:web@zip")
        webcontent("com.haulmont.workflow:workflow-web6:$cubaVersion:web@zip")
        webcontent("com.haulmont.reports:reports-web6:$cubaVersion:web@zip")
        webcontent("com.haulmont.thesis.app:thesis-web:$thesisVersion:web@zip")
        webcontent("com.haulmont.thesis.toolkit:thesis-web6-deps:$thesisVersion:web@zip")
        


        themes(group: 'com.haulmont.cuba', name: 'cuba-web6-themes', version: cubaVersion)
    }

    task webArchive(type: Zip) {
        from file('web')
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    task deployConf(type: Copy) {
        from file('src')
        include "com/company/thesisauto/**"
        into "$tomcatDir/conf/app"
    }

    task cleanConf(description: 'Cleans up conf directory') << {
        def dir = new File(tomcatDir, '/conf/app')
        if (dir.isDirectory()) {
            ant.delete(includeemptydirs: true) {
                fileset(dir: dir, includes: '**/*', excludes: 'local.web-app.properties')
            }
        }
    }
    def staticContentDir = "$tomcatDir/webapps/app/static"
    task deployThesisDocZip {
        def thesisDocZip = zipTree(configurations.staticContent.resolvedConfiguration.getResolvedArtifacts().find { it.name.startsWith('thesis-doc') }.file)
        inputs.file thesisDocZip
        outputs.dir new File(staticContentDir)
        doLast {

            copy {
                from thesisDocZip
                include 'quickstart/ru/html-single/**/*'
                include 'quickstart/ru/pdf/**/*'
                eachFile { details ->
                    details.path = details.path.replaceAll('quickstart/ru/html-single/', '')
                    details.path = details.path.replaceAll('quickstart/ru/pdf/', '')
                }
                into staticContentDir + "/manual_html"
                includeEmptyDirs = false
            }

            copy {
                from thesisDocZip
                into staticContentDir
                include 'admin/ru/pdf/**'
                include 'extended/ru/pdf/**'
                eachFile {
                    path = name
                }
                rename 'admin-manual.pdf', 'Admin_manual_full.pdf'
                rename 'extended-manual.pdf', 'User_manual_full.pdf'
                includeEmptyDirs = false
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: ThesisWebAppDeployment) {
        appName = 'app'
        appJars('thesis-web', 'thesis-gui', 'thesis-global', 'reports-global', 'reports-gui', 'reports-web', 'charts-gui', 'charts-web', 'charts-global', 'workflow-global', 'workflow-web', 'workflow-gui', 'fts-gui', 'fts-web', 'fts-global', 'cuba-web', 'cuba-gui', 'cuba-client', 'cuba-global', 
                'thesisauto-global', 'thesisauto-gui', 'thesisauto-web', 'thesis-shared-workflow')
    } << {
        deployThesisDocZip.execute()
    }
}

task undeploy(type: Delete, dependsOn: ':thesisauto-web:cleanConf') {
    delete("$tomcatDir/shared")
    delete("$tomcatDir/webapps/app-core")
    delete("$tomcatDir/webapps/app")
    delete("$tomcatDir/webapps/app-portal")
}

task restart(dependsOn: ['stop', ':thesisauto-core:deploy', ':thesisauto-web:deploy', ':thesisauto-portal:deploy', ':thesisauto-mobile:deploy', ':thesisauto-mobile-rest:deploy'], description: 'Redeploys applications and restarts local Tomcat') << {
    ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
        not {
            socket(server: 'localhost', port: '8787')
        }
    }
    start.execute()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
